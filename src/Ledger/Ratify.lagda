\section{Ratification}
\label{sec:ratification}
Governance actions are \defn{ratified} through on-chain voting actions.
Different kinds of governance actions have different ratification requirements
but always involve \textit{two of the three} governance bodies, with the exception
of a hard-fork initiation, which requires ratification by all governance bodies.
Depending on the type of governance action, an action will thus be ratified when
a combination of the following occurs:
\begin{itemize}
\item
  the \defn{constitutional committee} (\CC) approves of the action; for this to occur,
  the number of \CC members who vote \yes must meet the \CC vote threshold;
\item
  the \defn{delegation representatives} (\DReps) approve of the action; for this to occur,
  the stake controlled by the \DReps who vote \yes must meet the \DRep vote threshold
  as a percentage of the \defn{total participating voting stake} (\totalStake);
\item
  the stake pool operators (\SPOs) approve of the action; for this to occur,
  the stake controlled by the \SPOs who vote \yes must meet a certain threshold as a percentage of
  the \defn{total registered voting stake} (\totalStake).
\end{itemize}
\textbf{Warning}. Different stake distributions apply to \DReps and \SPOs.
\\[4pt]
A successful motion of no-confidence, election of a new constitutional
committee, a constitutional change, or a hard-fork delays ratification of
all other governance actions until the first epoch after their
enactment. This gives a new constitutional committee enough time to vote
on current proposals, re-evaluate existing proposals with respect to a
new constitution, and ensures that the in principle arbitrary semantic
changes caused by enacting a hard-fork do not have unintended
consequences in combination with other actions.

\subsection{Ratification requirements}
\label{sec:ratification-requirements}
Figure~\ref{fig:ratification-requirements} details the ratification requirements for each
governance action scenario. The columns represent
\begin{itemize}
\item
  \GovAction: the action under consideration;
\item
  \CC: a \ding{51} indicates that the constitutional committee must approve this action;
  a - symbol means that constitutional committee votes do not apply;
\item
  \DRep: the vote threshold that must be met as a percentage of \totalStake;
\item
  \SPO: the vote threshold that must be met as a percentage of the stake held by
  all stake pools; a - symbol means that \SPO votes do not apply.
\end{itemize}
\begin{figure*}[h!]
\begin{longtable}[]{@{}
   >{\raggedright\arraybackslash}p{(\columnwidth - 6\tabcolsep) * \real{0.65}}
  >{\raggedright\arraybackslash}p{(\columnwidth - 6\tabcolsep) * \real{0.11}}
  >{\raggedright\arraybackslash}p{(\columnwidth - 6\tabcolsep) * \real{0.12}}
  >{\raggedright\arraybackslash}p{(\columnwidth - 6\tabcolsep) * \real{0.12}}@{}}
\GovAction  & \CC  &  \DRep & \SPO \\
\hline
\endhead
1. Motion of no-confidence & - & \Pone & \Qone \\
2a. New committee/threshold (\emph{normal state}) & - & \Ptwoa & \Qtwoa \\
2b. New committee/threshold (\emph{state of no-confidence}) & - & \Ptwob & \Qtwob \\
3. Update to the Constitution & \ding{51} & \Pthree & - \\
4. Hard-fork initiation & \ding{51} & \Pfour & \Qfour \\
5a. Changes to protocol parameters in the \NetworkGroup & \ding{51} & \Pfivea & - \\
5b. Changes to protocol parameters in the \EconomicGroup & \ding{51} & \Pfiveb & - \\
5c. Changes to protocol parameters in the \TechnicalGroup & \ding{51} & \Pfivec & - \\
5d. Changes to protocol parameters in the \GovernanceGroup & \ding{51} & \Pfived & - \\
6. Treasury withdrawal & \ding{51} & \Psix & - \\
7. Info & \ding{51} & \(100\) & \(100\) \\
\end{longtable}
\caption{Ratification requirements}
\label{fig:ratification-requirements}
\end{figure*}
Each of these thresholds is a governance parameter.  The two thresholds for the \Info
action are set to 100\% since setting it any lower would result in not being able to poll
above the threshold.

\subsection{Ratification restrictions}
\label{sec:ratification-restrictions}
\begin{code}[hide]
{-# OPTIONS --safe #-}

open import Data.Integer using () renaming (+_ to _á¶»)
open import Data.Rational as â„š using (â„š; 0â„š; _/_; _â‰¥_)
open import Data.Nat.Properties hiding (_â‰Ÿ_; _â‰¤?_)
open import Data.Nat.Properties.Ext

open import Ledger.Prelude hiding (_âˆ§_; _/_; _â‰¥_)
open import Ledger.Transaction

module Ledger.Ratify (txs : _) (open TransactionStructure txs) where

open import Ledger.Gov govStructure

infixr 2 _âˆ§_
_âˆ§_ = _Ã—_

instance
  _ = +-0-commutativeMonoid
  _ = +-0-monoid
\end{code}
\begin{figure*}[h!]
{\small
\begin{code}
record StakeDistrs : Set where
  field stakeDistr  : VDeleg â‡€ Coin

record RatifyEnv : Set where
  field stakeDistrs   : StakeDistrs
        currentEpoch  : Epoch
        dreps         : Credential â‡€ Epoch
        ccHotKeys     : Credential â‡€ Maybe Credential
        treasury      : Coin

record RatifyState : Set where
  constructor âŸ¦_,_,_âŸ§Ê³
  field es              : EnactState
        removed         : â„™ (GovActionID Ã— GovActionState)
        delay           : Bool

CCData : Set
CCData = Maybe ((Credential â‡€ Epoch) Ã— â„š)

isCC : VDeleg â†’ Bool
isCC (credVoter CC _)  = true
isCC _                 = false

isDRep : VDeleg â†’ Bool
isDRep (credVoter DRep _)  = true
isDRep (credVoter _ _)     = false
isDRep abstainRep          = true
isDRep noConfidenceRep     = true

isSPO : VDeleg â†’ Bool
isSPO (credVoter SPO _)  = true
isSPO _                  = false
\end{code}
\begin{code}[hide]
isCCProp : specProperty Î» x â†’ isCC x â‰¡ true
isCCProp = to-sp (Î» x â†’ isCC x â‰Ÿ true)

isDRepProp : specProperty Î» x â†’ isDRep x â‰¡ true
isDRepProp = to-sp (Î» x â†’ isDRep x â‰Ÿ true)

isSPOProp : specProperty Î» x â†’ isSPO x â‰¡ true
isSPOProp = to-sp (Î» x â†’ isSPO x â‰Ÿ true)
\end{code}
} %% end small
\caption{Types and functions for the RATIFY transition system}
\label{fig:types-and-functions-for-the-ratify-transition-system}
\end{figure*}
As mentioned earlier, each \GovAction must include a \GovActionID
for the most recently enacted action of its given type. Consequently, two actions of the
same type can be enacted at the same time, but they must be \emph{deliberately}
designed to do so.

Figure~\ref{fig:types-and-functions-for-the-ratify-transition-system} defines three more types and some helper functions used in the ratification transition system.
\begin{itemize}
\item \StakeDistrs represents a map relating each voting delegate to an amount of stake;
\item \RatifyEnv denotes an environment with data required for ratification;
\item \RatifyState denotes an enactment state that exists during ratification;
\item \CCData stores data about the constitutional committee.
\item \isCC, \isDRep, and \isSPO, which return \true if the given delegate is a \CC member, a \DRep, or an \SPO (resp.) and \false otherwise.
\end{itemize}
\begin{code}[hide]
-- TODO: remove these or put them into RatifyState
coinThreshold rankThreshold : â„•
coinThreshold = 1000000000
rankThreshold = 1000

-- DReps with at least `c` coins
mostStakeDRepDist : Credential â‡€ Coin â†’ Coin â†’ Credential â‡€ Coin
mostStakeDRepDist dist c = dist â†¾' to-sp (_â‰¥? c)

-- mostStakeDRepDist-homomorphic : âˆ€ {dist} â†’ Homomorphicâ‚‚ _ _ _>_ (_âŠ†_ on _Ë¢) (mostStakeDRepDist dist)
-- mostStakeDRepDist-homomorphic x>y = implâ‡’coresâŠ† _ _ {!!} --(<-trans x>y)

mostStakeDRepDist-0 : âˆ€ {dist} â†’ mostStakeDRepDist dist 0 â‰¡áµ‰áµ dist
mostStakeDRepDist-0 = (projâ‚‚ âˆ˜ Equivalence.from âˆˆ-filter)
                    , Î» x â†’ Equivalence.to âˆˆ-filter (zâ‰¤n , x)

-- TODO: maybe this can be proven easier with the maximum?
mostStakeDRepDist-âˆ… : âˆ€ {dist} â†’ âˆƒ[ N ] mostStakeDRepDist dist N Ë¢ â‰¡áµ‰ âˆ…
mostStakeDRepDist-âˆ… {dist} = suc (Î£áµáµ›[ x â† dist á¶ áµ ] x) , Properties.âˆ…-least
  (âŠ¥-elim âˆ˜ uncurry helper âˆ˜ Equivalence.from âˆˆ-filter)
  where
    open â‰¤-Reasoning

    helper : âˆ€ {k v} â†’ v > Î£áµáµ›[ x â† dist á¶ áµ ] x â†’ (k , v) âˆ‰ dist
    helper {k} {v} v>sum kvâˆˆdist = 1+nâ‰°n $ begin-strict
      v
        â‰¡Ë˜âŸ¨ indexedSum-singleton' $ finiteness â´ k , v âµ âŸ©
      Î£áµáµ›[ x â† â´ k , v âµáµ á¶ áµ ] x
        â‰¡Ë˜âŸ¨ indexedSumáµ-cong {x = (dist âˆ£ â´ k âµ) á¶ áµ} {y = â´ k , v âµáµ á¶ áµ}
          $ res-singleton' {m = dist} kvâˆˆdist âŸ©
      Î£áµáµ›[ x â† (dist âˆ£ â´ k âµ) á¶ áµ ] x
        â‰¤âŸ¨ mâ‰¤m+n _ _ âŸ©
      Î£áµáµ›[ x â† (dist âˆ£ â´ k âµ) á¶ áµ ] x +â„• Î£áµáµ›[ x â† (dist âˆ£ â´ k âµ á¶œ) á¶ áµ ] x
        â‰¡Ë˜âŸ¨ indexedSumáµ-partition {m = dist á¶ áµ} {(dist âˆ£ â´ k âµ) á¶ áµ} {(dist âˆ£ â´ k âµ á¶œ) á¶ áµ}
          $ res-ex-disj-âˆª Properties.Dec-âˆˆ-singleton âŸ©
      Î£áµáµ›[ x â† dist á¶ áµ ] x
        <âŸ¨ v>sum âŸ©
      v âˆ

âˆƒtopNDRepDist : âˆ€ {n dist} â†’ n â‰¤ lengthË¢ (dist Ë¢) â†’ n > 0
                â†’ âˆƒ[ c ] n â‰¤ lengthË¢ (mostStakeDRepDist dist c Ë¢)
                       Ã— lengthË¢ (mostStakeDRepDist dist (suc c) Ë¢) < n
âˆƒtopNDRepDist {n} {dist} lengthâ‰¥n n>0 =
  let
    c , h , h' =
      negInduction (Î» _ â†’ _ â‰¥? n)
        (subst (n â‰¤_) (sym $ lengthË¢-â‰¡áµ‰ _ _ (mostStakeDRepDist-0 {dist})) lengthâ‰¥n)
        (mapâ‚‚â€² (Î» h h'
                  â†’ â‰¤â‡’â‰¯ (subst (n â‰¤_) (trans (lengthË¢-â‰¡áµ‰ _ _ h) lengthË¢-âˆ…) h') n>0)
               (mostStakeDRepDist-âˆ… {dist}))
  in
   c , h , â‰°â‡’> h'

topNDRepDist : â„• â†’ Credential â‡€ Coin â†’ Credential â‡€ Coin
topNDRepDist n dist = case (lengthË¢ (dist Ë¢) â‰¥? n) ,â€² (n >? 0) of Î» where
  (_     , no  _)  â†’ âˆ…áµ
  (no _  , yes _)  â†’ dist
  (yes p , yes pâ‚) â†’ mostStakeDRepDist dist (projâ‚ (âˆƒtopNDRepDist {dist = dist} p pâ‚))

-- restrict the DRep stake distribution
-- commented out for now, since we don't know if that'll actually be implemented
restrictedDists : â„• â†’ â„• â†’ StakeDistrs â†’ StakeDistrs
restrictedDists coins rank dists = dists
  -- record dists { drepStakeDistr = restrict drepStakeDistr }
  where open StakeDistrs dists
        -- one always includes the other
        restrict : Credential â‡€ Coin â†’ Credential â‡€ Coin
        restrict dist = topNDRepDist rank dist âˆªáµË¡ mostStakeDRepDist dist coins
\end{code}
\begin{figure*}[h!]
{\small
\begin{code}
actualVotes : RatifyEnv â†’ CCData â†’ (GovRole Ã— Credential) â‡€ Vote â†’ GovAction â†’ PParams
            â†’ VDeleg â‡€ Vote
actualVotes Î“ cc votes ga pparams
  =    mapKeys (credVoter CC) actualCCVotes
  âˆªáµË¡  actualPDRepVotes âˆªáµË¡ actualDRepVotes
  âˆªáµË¡  actualSPOVotes
  where
    open RatifyEnv Î“
    open PParams pparams

    roleVotes : GovRole â†’ VDeleg â‡€ Vote
    roleVotes r = mapKeys (uncurry credVoter) (filteráµ? ((r â‰Ÿ_) âˆ˜ projâ‚ âˆ˜ projâ‚) votes)

    activeCC activeDReps : â„™ Credential
    activeCC = case cc of Î» where
      (just (cc , _))  â†’ dom (filteráµáµ‡ (is-just âˆ˜ projâ‚‚) (ccHotKeys âˆ£ dom cc))
      nothing          â†’ âˆ…

    activeDReps = dom (filteráµ? (Î» x â†’ currentEpoch â‰¤? (projâ‚‚ x)) dreps)

    actualCCVote : Credential â†’ Epoch â†’ Vote
    actualCCVote c e =
      case Â¿ currentEpoch â‰¤ e Â¿áµ‡ , lookupáµ? ccHotKeys c of Î» where
        (true , just (just c'))  â†’ maybe id Vote.no $ lookupáµ? votes (CC , c')
        _                        â†’ Vote.abstain -- expired, no hot key or resigned

    actualCCVotes : Credential â‡€ Vote
    actualCCVotes = case cc , Â¿ ccMinSize â‰¤ lengthË¢ activeCC Â¿áµ‡ of Î» where
      (just (cc , _)  , true)   â†’ mapWithKey actualCCVote cc
      (just (cc , _)  , false)  â†’ constMap (dom cc) Vote.no
      (nothing        , _)      â†’ âˆ…áµ

    actualPDRepVotes
      =    â´ abstainRep       , Vote.abstain âµáµ
      âˆªáµË¡  â´ noConfidenceRep  , (case ga of Î» where  NoConfidence  â†’ Vote.yes
                                                     _             â†’ Vote.no) âµáµ

    actualDRepVotes
      =    roleVotes GovRole.DRep
      âˆªáµË¡  constMap (mapË¢ (credVoter DRep) activeDReps) Vote.no

    actualSPOVotes
      =    roleVotes GovRole.SPO
      âˆªáµË¡  constMap spos (if isHF then Vote.no else Vote.abstain)
      where
        spos : â„™ VDeleg
        spos = filterË¢ isSPOProp $ dom (StakeDistrs.stakeDistr stakeDistrs)

        isHF : Bool
        isHF = case ga of Î» where
          (TriggerHF _)  â†’ true
          _              â†’ false
\end{code}
} %% end small
\caption{%Ratify i:
Types and proofs for the ratification of governance actions}
\label{fig:defs:ratify-i}
\end{figure*}
The code in Figure~\ref{fig:defs:ratify-i} defines some of the types required for ratification of a governance action.
\begin{itemize}
  \item Assuming a ratification environment \AgdaPostulate{Î“},
  \begin{itemize}
    \item \agdaboundcc contains constitutional committee data;
    \item \agdaboundvotes is a relation associating each role-credential pair with the vote cast by the individual denoted by that pair;
    \item \agdaboundga denotes the governance action being voted upon.
  \end{itemize}

  \item \roleVotes filters the votes based on the given governance role.

  \item \actualCCVote determines how the vote of each \CC member will be counted; specifically, if a \CC member has not yet registered a hot key, has
  \expired, or has resigned, then \actualCCVote returns \abstain;
  if those none of these conditions is met, then
  \begin{itemize}
    \item if the \CC member has voted, then that vote is returned;
    \item if the \CC member has not voted, then the default value of \no is returned.
  \end{itemize}

  \item \actualCCVotes uses \actualCCVote to determine how the votes of all \CC members will be counted.

  \item \actualPDRepVotes determines how the votes will be counted for \DReps;
  here, \abstainRep is mapped to \abstain and \noConfidenceRep is mapped to either \yes or \no,
  depending on the value of \agdaboundga.

  \item \actualDRepVotes determines how the votes of \DReps will be counted; \activeDReps that didn't vote count as a \no.

  \item \actualVotes is a partial function relating delegates to the actual vote that will be counted on their behalf;
  it accomplishes this by aggregating the results of \actualCCVotes, \actualPDRepVotes, and \actualDRepVotes.
\end{itemize}
\begin{figure*}[h!]
{\small
\begin{code}
votedHashes : Vote â†’ (VDeleg â‡€ Vote) â†’ GovRole â†’ â„™ VDeleg
votedHashes v votes r = votes â»Â¹ v

votedYesHashes : (VDeleg â‡€ Vote) â†’ GovRole â†’ â„™ VDeleg
votedYesHashes = votedHashes Vote.yes

votedAbstainHashes participatingHashes : (VDeleg â‡€ Vote) â†’ GovRole â†’ â„™ VDeleg
votedAbstainHashes = votedHashes Vote.abstain
participatingHashes votes r = votedYesHashes votes r âˆª votedHashes Vote.no votes r
\end{code}
} %% end small
\caption{Calculation of the votes as they will be counted}
\label{fig:defs:ratify-ii}
\end{figure*}

The code in Figure~\ref{fig:defs:ratify-ii} defines \votedHashes, which returns the set of delegates who voted a certain way on the given governance role.
\begin{figure*}[h!]
{\small
\begin{code}[hide]
abstract
\end{code}
\begin{code}
  getStakeDist : GovRole â†’ â„™ VDeleg â†’ StakeDistrs â†’ VDeleg â‡€ Coin
  getStakeDist CC    cc  _                             = constMap (filterË¢ isCCProp cc) 1
  getStakeDist DRep  _   record { stakeDistr = dist }  = filteráµ (sp-âˆ˜ isDRepProp  projâ‚) dist
  getStakeDist SPO   _   record { stakeDistr = dist }  = filteráµ (sp-âˆ˜ isSPOProp   projâ‚) dist

  acceptedStake : GovRole â†’ â„™ VDeleg â†’ StakeDistrs â†’ (VDeleg â‡€ Vote) â†’ Coin
  acceptedStake r cc dists votes =
    Î£áµáµ›[ x â† (getStakeDist r cc dists âˆ£ votedYesHashes votes r) á¶ áµ ] x

  totalStake : GovRole â†’ â„™ VDeleg â†’ StakeDistrs â†’ (VDeleg â‡€ Vote) â†’ Coin
  totalStake r cc dists votes =
    Î£áµáµ›[ x  â† getStakeDist r cc dists âˆ£ votedAbstainHashes votes r á¶œ á¶ áµ ] x

  activeVotingStake : â„™ VDeleg â†’ StakeDistrs â†’ (VDeleg â‡€ Vote) â†’ Coin
  activeVotingStake cc dists votes =
    Î£áµáµ›[ x  â† getStakeDist DRep cc dists âˆ£ dom votes á¶œ á¶ áµ ] x

  expired : Epoch â†’ GovActionState â†’ Set
  expired current record { expiresIn = expiresIn } = expiresIn < current

  acceptedBy : RatifyEnv â†’ EnactState â†’ GovActionState â†’ GovRole â†’ Set
  acceptedBy Î“ es gs role = stakeFilter (totalStake role cc' redStakeDistr votes')
    where
    open RatifyEnv Î“
    open GovActionState gs
    open EnactState es renaming (cc to ğ•”ğ•”; pparams to ğ•¡ğ•¡)
    cc : Maybe ((Credential â‡€ Epoch) Ã— â„š)
    cc = projâ‚ ğ•”ğ•”

    pparams : PParams
    pparams = projâ‚ ğ•¡ğ•¡

    votes' : Î£ (Rel VDeleg Vote) left-unique
    votes' = actualVotes Î“ cc votes action pparams

    cc' : â„™ VDeleg
    cc' = dom votes'

    redStakeDistr : StakeDistrs
    redStakeDistr = restrictedDists coinThreshold rankThreshold stakeDistrs

    t : â„š
    t = maybe id â„š.0â„š $ threshold pparams (projâ‚‚ <$> cc) action role

    stakeFilter : Coin â†’ Set
    stakeFilter zero = t â‰¡ â„š.0â„š
    stakeFilter (suc x) = (acceptedStake role cc' redStakeDistr votes')á¶» / suc x â‰¥ t

  accepted : RatifyEnv â†’ EnactState â†’ GovActionState â†’ Set
  accepted Î“ es gs = acceptedBy Î“ es gs CC âˆ§ acceptedBy Î“ es gs DRep âˆ§ acceptedBy Î“ es gs SPO

\end{code}
} %% end small
\caption{%%Ratify iii:
Calculation of stake distributions}
\label{fig:defs:ratify-iii}
\end{figure*}
The code in Figure~\ref{fig:defs:ratify-iii} defines yet more types required for ratification of a governance action.
\begin{itemize}
  \item \getStakeDist computes the stake distribution based on the given governance role and the corresponding delegations;
  \item \acceptedStake calculates the sum of stakes for all delegations that voted \yes for the specified role;
  \item \totalStake calculates the sum of stakes for all delegations that didn't vote \abstain for the given role;
  \item \activeVotingStake computes the total stake for the role of \DRep for active voting; it calculates the sum of
  stakes for all active delegates that have not voted (i.e., their delegation is present in \CC but not in the \agdaboundvotes mapping);
  \item \accepted checks if an action is accepted for the \CC, \DRep, and \SPO roles and whether it meets the minimum active voting stake (\meetsMinAVS);
  \item \expired checks whether a governance action is expired in a given epoch.
\end{itemize}
\begin{figure*}[h!]
{\small
\begin{code}[hide]
open EnactState
\end{code}
\begin{code}
verifyPrev : (a : GovAction) â†’ NeedsHash a â†’ EnactState â†’ Set
verifyPrev NoConfidence           h es  = h â‰¡ es .cc .projâ‚‚
verifyPrev (NewCommittee _ _ _)   h es  = h â‰¡ es .cc .projâ‚‚
verifyPrev (NewConstitution _ _)  h es  = h â‰¡ es .constitution .projâ‚‚
verifyPrev (TriggerHF _)          h es  = h â‰¡ es .pv .projâ‚‚
verifyPrev (ChangePParams _)      h es  = h â‰¡ es .pparams .projâ‚‚
verifyPrev (TreasuryWdrl _)       _ _   = âŠ¤
verifyPrev Info                   _ _   = âŠ¤

delayingAction : GovAction â†’ Bool
delayingAction NoConfidence           = true
delayingAction (NewCommittee _ _ _)   = true
delayingAction (NewConstitution _ _)  = true
delayingAction (TriggerHF _)          = true
delayingAction (ChangePParams _)      = false
delayingAction (TreasuryWdrl _)       = false
delayingAction Info                   = false

delayed : (a : GovAction) â†’ NeedsHash a â†’ EnactState â†’ Bool â†’ Set
delayed a h es d = Â¬ verifyPrev a h es âŠ d â‰¡ true
\end{code}
\begin{code}[hide]
abstract
  verifyPrev? : âˆ€ a h es â†’ Dec (verifyPrev a h es)
  verifyPrev? NoConfidence           h es = it
  verifyPrev? (NewCommittee x xâ‚ xâ‚‚) h es = it
  verifyPrev? (NewConstitution x xâ‚) h es = it
  verifyPrev? (TriggerHF x)          h es = it
  verifyPrev? (ChangePParams x)      h es = it
  verifyPrev? (TreasuryWdrl x)       h es = it
  verifyPrev? Info                   h es = it

  delayed? : âˆ€ a h es d â†’ Dec (delayed a h es d)
  delayed? a h es d = let instance _ = verifyPrev? a h es in it

  acceptedBy? : âˆ€ Î“ es st role â†’ Dec (acceptedBy Î“ es st role)
  acceptedBy? Î“ record{ cc = cc , _ ; pparams = pparams , _ } st role
    with (let open RatifyEnv Î“; open GovActionState st; open PParams pparams
              votes' = actualVotes Î“ cc votes action pparams
              cc' = dom votes'
              redStakeDistr = restrictedDists coinThreshold rankThreshold stakeDistrs
          in totalStake role cc' redStakeDistr votes')
  ... | zero  = it
  ... | suc n = it

  accepted? : âˆ€ Î“ es st â†’ Dec (accepted Î“ es st)
  accepted? Î“ es st =
    let instance _ = Î» {role} â†’ acceptedBy? Î“ es st role
    in it

  expired? : âˆ€ e st â†’ Dec (expired e st)
  expired? e st = Â¿ expired e st Â¿
\end{code}
} %% end small
\caption{%Ratify iv:
Determination of the status of ratification of the governance action}
\label{fig:defs:ratify-iv}
\end{figure*}
The code in Figure~\ref{fig:defs:ratify-iv} defines still more types required for ratification of a governance action.
\begin{itemize}
  \item \verifyPrev takes a governance action, its \NeedsHash, and \EnactState and checks whether the ratification restrictions are met;
  \item \delayingAction takes a governance action and returns \true if it is a ``delaying action'' (\NoConfidence, \NewCommittee, \NewConstitution, \TriggerHF)
  and returns \false otherwise;
  \item \delayed checks whether a given \GovAction is delayed.
\end{itemize}
\begin{code}[hide]
private variable
  Î“ : RatifyEnv
  es es' : EnactState
  a : GovActionID Ã— GovActionState
  removed : â„™ (GovActionID Ã— GovActionState)
  d : Bool

data _âŠ¢_â‡€â¦‡_,RATIFY'â¦ˆ_ : RatifyEnv â†’ RatifyState â†’ GovActionID Ã— GovActionState â†’ RatifyState â†’ Set where

\end{code}
\begin{figure*}[h!]
{\small
\begin{code}
  RATIFY-Accept : let open RatifyEnv Î“; st = a .projâ‚‚; open GovActionState st in
       accepted Î“ es st
    â†’  Â¬ delayed action prevAction es d
    â†’  âŸ¦ a .projâ‚ , treasury , currentEpoch âŸ§áµ‰ âŠ¢ es â‡€â¦‡ action ,ENACTâ¦ˆ es'
       â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
       Î“ âŠ¢  âŸ¦ es   , removed          , d                      âŸ§Ê³ â‡€â¦‡ a ,RATIFY'â¦ˆ
            âŸ¦ es'  , â´ a âµ âˆª removed  , delayingAction action  âŸ§Ê³

  RATIFY-Reject : let open RatifyEnv Î“; st = a .projâ‚‚ in
       Â¬ accepted Î“ es st
    â†’  expired currentEpoch st
       â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
       Î“ âŠ¢ âŸ¦ es , removed , d âŸ§Ê³ â‡€â¦‡ a ,RATIFY'â¦ˆ âŸ¦ es , â´ a âµ âˆª removed , d âŸ§Ê³

  RATIFY-Continue : let open RatifyEnv Î“; st = a .projâ‚‚; open GovActionState st in
       Â¬ accepted Î“ es st Ã— Â¬ expired currentEpoch st
    âŠ  accepted Î“ es st
       Ã— ( delayed action prevAction es d
         âŠ (âˆ€ es' â†’ Â¬ âŸ¦ a .projâ‚ , treasury , currentEpoch âŸ§áµ‰ âŠ¢ es â‡€â¦‡ action ,ENACTâ¦ˆ es'))
    â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
    Î“ âŠ¢ âŸ¦ es , removed , d âŸ§Ê³ â‡€â¦‡ a ,RATIFY'â¦ˆ âŸ¦ es , removed , d âŸ§Ê³

_âŠ¢_â‡€â¦‡_,RATIFYâ¦ˆ_  : RatifyEnv â†’ RatifyState â†’ List (GovActionID Ã— GovActionState)
                 â†’ RatifyState â†’ Set
_âŠ¢_â‡€â¦‡_,RATIFYâ¦ˆ_ = SSâ‡’BS _âŠ¢_â‡€â¦‡_,RATIFY'â¦ˆ_
\end{code}
} %% end small
\caption{The RATIFY transition system}
\label{fig:ratify-transition-system}
\end{figure*}
Figure~\ref{fig:ratify-transition-system} defines three rules,
\RATIFYAccept, \RATIFYReject, and \RATIFYContinue, along with the relation \RATIFYsyntax.
The latter is the transition relation for ratification of a \GovAction.
\begin{itemize}
  \item \RATIFYAccept checks if the votes for a given \GovAction meet the threshold required for
        acceptance, that the action is accepted and not delayed,
        and \RATIFYAccept ratifies the action.

  \item \RATIFYReject asserts that the given \GovAction is not \accepted and \expired;
        it removes the governance action.
  \item \RATIFYContinue covers the remaining cases and keeps the \GovAction around for further voting.
\end{itemize}

Note that all governance actions eventually either get accepted and enacted via \RATIFYAccept or
rejected via \RATIFYReject. If an action satisfies all criteria to be accepted but cannot be
enacted anyway, it is kept around and tried again at the next epoch boundary.

We never remove actions that do not attract sufficient \yes votes before they expire, even if it
is clear to an outside observer that this action will never be enacted. Such an action will simply
keep getting checked every epoch until it expires.
